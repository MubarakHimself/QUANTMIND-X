---
title: Information Storage and View
url: https://www.mql5.com/en/articles/1405
categories: Trading, Integration, Expert Advisors
relevance_score: 0
scraped_at: 2026-01-24T13:43:46.286541
---

[![](https://www.mql5.com/ff/sh/0uquj7zv5pmx2m6kz2/c0d1e95edf776bf88908b398733d0997.jpg)\\
Market analytics in MQL5 Channels\\
\\
Tens of thousands of traders have chosen this messaging app to receive trading tips.\\
\\
Download](https://www.mql5.com/ff/go?link=https://www.metatrader5.com/en/news/2270%3Futm_source=www.mql5.com%26utm_medium=display%26utm_term=messenger.for.traders%26utm_content=download.app%26utm_campaign=0524.mql5.channels&a=epadtzgppsywkaeumqycnulasoijfbgz&s=9615c3e5c371aa0d7b34529539d05c10df73b35a1e2213e4ceee008933c7ede0&uid=&ref=https://www.mql5.com/en/articles/1405&id=bfogggabsofabcpxuzmgaibarmaxasdrj&fz_uniq=5083071229407007841)

MetaTrader 4 / Trading


### 1\. Introduction

Have you ever spent hours of time trying to find some important information previously
providently written by your expert into a log file? Or, perhaps, you are fully
tired of peering at the small one-color letters displayed by the Comment() function?
And yet, they can be of great importance for trading. If you know what I mean,
this article is intended for you.

Below are the main problems I posed for myself writing this article:

- How to create a function that can be used by the expert in order to write information
into its native log file (the counterpart of the Print() function)?

- How to create a tool that can make the displayed information have better visual
impact: color and font size changes?


### 2\. The Expert's Native Log File

As I said before, the Print() function is not always convenient for writing of information
generated by the expert during its operation. This is especially evident when several
experts trade in the same terminal simultaneously. Each expert writes its own information
into the log file, and then it is rather difficult to find something there. In
this case, the constructive analysis of such information is absolutely out of the
question: it is very laborious and cumbersome.

This problem can be solved in a rather simple way. Every expert must have its own
log file. And the information will be written in this separate log file of the
expert, not in the general one. To be able to use this opportunity as easily as
possible, let us write the code as a function.

Well, it is described below what the function must do:

- create a file with a unique name

- write information in it, as and when necessary

- close the file as soon as the expert has finished operation in order the file to
be available for other applications


Everything is clear. The only arguable thing is the necessity to close the file
after every record. On the one hand, this would allow opening of the file in another
appplication during the expert operation. But, on the other hand, it could be harmful
if the expert would not be able to open the file to make the next record, since
the file could be used by another application. In this case, information can be
simply lost. This may not be allowed, especially since there are programs that
can open files for reading only, without disturbing MetaTrader to work with them.

As opening and closing of the file is performed only once, the responsible code
will be placed into the init() and deinit() functions, respectively. To make it
occupy the minimum space, let us represent it as functions, as well:

```
int log_handle = -1;

//+------------------------------------------------------------------+
// void log_open( string ExpertName = "Expert" )
//
// Function that opens the expert's separate log file.
// Directory, in which the file will be created:
// "...\MetaTrader 4\experts\files\logs\ExpertName\"
// The file name is the date of the file record appeared as "YYYY.MM.DD"
//+------------------------------------------------------------------+
void log_open( string ExpertName = "Expert" )
 {
     string log_name = "logs\\" + ExpertName + " (" + Symbol() + ", " +
                   strPeriod( Period() ) + ")\\" + TimeToStr( LocalTime(),
                   TIME_DATE ) + ".txt";
  log_handle = FileOpen ( log_name, FILE_READ | FILE_WRITE, " " );

  if ( log_handle < 0 )
     {
         int _GetLastError = GetLastError();
    Print( "FileOpen( ", log_name, ", FILE_READ | FILE_WRITE, \" \" ) - Error #",
                     _GetLastError );
    return(-1);
   }
 }
string strPeriod( int intPeriod )
 {
     switch ( intPeriod )
     {
          case PERIOD_MN1: return("Monthly");
    case PERIOD_W1:  return("Weekly");
    case PERIOD_D1:  return("Daily");
    case PERIOD_H4:  return("H4");
    case PERIOD_H1:  return("H1");
    case PERIOD_M30: return("M30");
    case PERIOD_M15: return("M15");
    case PERIOD_M5:  return("M5");
    case PERIOD_M1:  return("M1");
    default:        return("UnknownPeriod");
   }
 }

//+------------------------------------------------------------------+
// log_close()
//
// Function that closes the expert's native log file.
//+------------------------------------------------------------------+
void log_close()
 {
     if ( log_handle > 0 ) FileClose( log_handle );
 }
```

Now we have an open file, information can be written into it. To do so:

- move the cursor to the end of the file in order not to lose the information;

- substitute the recording time to the beginning of the line; this will be useful
for analysis;

- write the text into the file;

- store the completed file on the disk; if the expert terminates its operation disorderly,
this will prevent data losses


The following function must result from the above:

```
//+------------------------------------------------------------------+
// log( string text )
//
// The function that writes the text line into the expert's native log file.
//+------------------------------------------------------------------+
void log( string text )
 {
     int _GetLastError = 0;
  if ( log_handle < 0 )
     {
         Print( "Log write error! Text: ", text );
    return(-1);
   }

     //---- Move the file pointer to the end of file
    if ( !FileSeek ( log_handle, 0, SEEK_END ) )
      {
         _GetLastError = GetLastError();
    Print( "FileSeek ( " + log_handle + ", 0, SEEK_END ) - Error #",
          _GetLastError );
    return(-1);
  }
    //---- If the line to be written by the expert is not the line feed character,
    //---- add the recording time at the beginning of the line
    if( text != "\n" && text != "\r\n" )
         text = StringConcatenate( TimeToStr( LocalTime(), TIME_SECONDS ),
                             " - - - ", text );
    if( FileWrite ( log_handle, text ) < 0 )
          {
             _GetLastError = GetLastError();
     Print( "FileWrite ( ", log_handle, ", ", text, " ) - Error #",
           _GetLastError );
     return(-1);
    }

    //---- Save the written text on the disk
    FileFlush( log_handle );
 }
```

Now, the word Print can be easily replaced with the word log in all experts, calls
of the log\_open and log\_close functions not to be forgotten.

This is a simple exemplary expert using the included file log.mq4:

```
#include <log.mq4>

int init()
  {
    log_open( "log_test" );
    log( "The log file has been successfully opened, the expert starts working..." );
    return(0);
  }
int deinit()
  {
    log( "Close the log file, the expert finishes work..." );
    log_close();
    return(0);
  }
int start()
  {
    log( "New tick: Bid = " + DoubleToStr( Bid, Digits ) );
    return(0);
  }
```

### 3\. Information Displaying

Now, when we have solved the problem of the log file, we can start "decorating"
the displayed information.

First of all, let us consider all possible ways of how to implement this task. In
MQL4, the Comment() function is responsible for displaying of the information,
but it does not suit for the reasons described above. So we have to find another
solution. A good example of this can be objects containing texts. There are only
two of them: "Text" and "Text Label". The fundamental difference
between them is that the "Text" is anchored to the chart coordinates
(price and time), and the "Text Label" is to the windows coordinates.
Since we need information to remain on the spot when the chart moves or its scale
is changed, this will be the "Text Label" that we will use.

There are some functions in MQL4 that create and control objects, all names starting
with the word Object. Let us see what of them can be useful for our purposes:

- bool ObjectCreate(...) – to create an object;

- bool ObjectDelete(...) – to delete objects after usage;

- bool ObjectSet(...) – to change the object properties, such as anchor (x,y);

- bool ObjectSetText(...) – to display the text;

- void ObjectsRedraw() – to redraw objects after the text has been changed.


Well, this is what we have to do:

- in the expert function named init(), create objects to display information;

- the the expert function named deinit(), delete all created objects;

- in the start() function, be able to change text, font color and size of all created
objects.


We will get 3 functions again, each completing its own task.

Before writing the code, I would like to mention an unpleasant limitation concerning
usage of the "Text Label". It can be only of one line, i.e., it may not
contain any line feed characters. But information is acquired much better if it
is displayed in several lines. This is why we will create several objects and then
distribute the data among them. I made five "lines", but you can use
any other amounts of lines.

Besides, there is a limitation concerning the length of the displayed text. So I
have added the second "column", i.e., five more lines on the right-hand
side.

This is how the function info\_init() that creates objects looks:

```
/////////////////////////////////////////////////////////////////////////////////
// void info_init()
//
// Creation of objects to display information
/////////////////////////////////////////////////////////////////////////////////
void info_init()
  {
    for( int row = 0; row <= 4; row ++ )
      {
        _LabelCreate( StringConcatenate( "InfoLabel_0", row ),   4, 15 + 15*row );
        _LabelCreate( StringConcatenate( "InfoLabel_1", row ), 270, 15 + 15*row );
      }
  }

/////////////////////////////////////////////////////////////////////////////////
// void _LabelCreate ( string _Name, int _XDistance, int _YDistance, int _Corner = 0 )
//
// Creation of "Text Label" named as _Name.
// Coordinates: х = _XDistance, у = _YDistance, corner = _Corner.
/////////////////////////////////////////////////////////////////////////////////
void _LabelCreate ( string _Name, int _XDistance, int _YDistance, int _Corner = 0 )
  {
    int _GetLastError;

    if( !ObjectCreate( _Name, OBJ_LABEL, 0, 0, 0 ) )
      {
        _GetLastError = GetLastError();
        if ( _GetLastError != 4200 )
          {
            Print( "ObjectCreate( \"", _Name, "\", OBJ_LABEL,0,0,0 ) - Error #",
                   _GetLastError );
            return(-1);
          }
      }
    if( !ObjectSet( _Name, OBJPROP_CORNER, _Corner ) )
      {
        _GetLastError = GetLastError();
        Print( "ObjectSet( \"", _Name, "\", OBJPROP_CORNER, ", _Corner,
                                       " ) - Error #", _GetLastError );
      }
    if( !ObjectSet( _Name, OBJPROP_XDISTANCE, _XDistance ) )
      {
        _GetLastError = GetLastError();
        Print( "ObjectSet( \"", _Name, "\", OBJPROP_XDISTANCE, ", _XDistance,
                                             " ) - Error #", _GetLastError );
      }
    if( !ObjectSet( _Name, OBJPROP_YDISTANCE, _YDistance ) )
      {
        _GetLastError = GetLastError();
        Print( "ObjectSet( \"", _Name, "\", OBJPROP_YDISTANCE, ", _YDistance,
                                             " ) - Error #", _GetLastError );
      }
    if( !ObjectSetText ( _Name, "", 10 ) )
      {
        _GetLastError = GetLastError();
        Print( "ObjectSetText( \"", _Name, "\", \"\", 10 ) - Error #", _GetLastError );
      }
  }
```

As you can see, objects will be named as "InfoLabel\_" + object number (from 00 to 04 for
the left "column" and from 10 to 14 for the right one). The objects are anchored to the upper left
corner. A space is intentionally made before, since many users got used to see OHLC (the current bar information)
there. I made the vertical space between lines equal to 15, this is enough for a text of a normal size. So, initialization
is over, let us make the deinitialization. They will be rather similar:

```
/////////////////////////////////////////////////////////////////////////////////
// void info_deinit()
//
// Deletion of objects created by the info_init() function
/////////////////////////////////////////////////////////////////////////////////
void info_deinit()
 {
     int _GetLastError;
   for ( int row = 0; row <= 4; row ++ )
      {
          if ( !ObjectDelete( StringConcatenate( "InfoLabel_0", row ) ) )
           {
                   _GetLastError = GetLastError();
       Print( "ObjectDelete( \"", StringConcatenate( "InfoLabel_0", row ),
                                        "\" ) - Error #", _GetLastError );
      }
          if( !ObjectDelete( StringConcatenate( "InfoLabel_1", row ) ) )
               {
                  _GetLastError = GetLastError();
       Print( "ObjectDelete( \"", StringConcatenate( "InfoLabel_1", row ),
                                       "\" ) - Error #", _GetLastError );
      }
       }
 }
```

Now, the most interesting thing is the information displaying itself.

Thus, we have 10 objects, which are each on their places and ready to "accept"
the text to be displayed. Let us think of how to make the use of the displaying
function as simple as possible.

First, it is simpler to specify the number, not the name of the object.

Second, only two parameters must be used: the object number and the displayed text
number. Other parameters can be optional since the text color or the font size
do not often need to be changed. Here, we have to think over what to do if the
parameters are skipped. There are two choices:

- to use parameters by default;

- or to use the last used parameters.


I think the second choice to be more useful: The default parameters would need being
changed in the code for every expert, and the last used parameters will be saved
automatically at every use of the function.

Thus, this is how our function looks:

```
void info( int LabelNumber, string Text, color Color = -1,
              double FontSize = -1.0, string Font = "-1" )
 {
     //---- define the object name
      string LabelName;
  if ( LabelNumber < 10 )
          LabelName = StringConcatenate( "InfoLabel_0", LabelNumber );
  else
         LabelName = StringConcatenate( "InfoLabel_" , LabelNumber );

  //---- if the additional parameters were not specified,
    //---- set the last used values for them
    if ( Color < 0 )
          Color = lastusedColor;
  if ( FontSize < 0 )
     FontSize = lastusedFontSize;
  if ( Font == "-1" )
     Font = lastusedFont;

  //---- save the last used values
    lastusedColor = Color;
  lastusedFontSize = FontSize;
  lastusedFont = Font;
```

To avoid situations where the last used variables contain the null as a value, let
us assign them their values immediately at declaration:

```
color lastusedColor = Black;
double lastusedFontSize = 9.0;
string lastusedFont = "Arial";
```

You could probably note that the last used variables are declared out of functions.
This will prevent their values from zeroizing at every info() function call.

And now, let us display the new text and redraw the objects. This must be done for
the changes that we have made to the object to be displayed immediately:

```
 //---- display the new text
    if( !ObjectSetText( LabelName, Text, FontSize, Font, Color ) )
      {
        int _GetLastError = GetLastError();
        Print( "ObjectSetText( \"", LabelName,"\", \"", Text, "\", ", FontSize, ", ", Font,
                                             ", ", Color, " ) - Error #", _GetLastError );
      }
    //---- redraw the objects
    ObjectsRedraw();
  }
```

The full code of all three functions can be found in the attached file named info.mq4.

Now, let us check what we have got:

**![](https://c.mql5.com/2/13/info1_eng.gif)**

**![](https://c.mql5.com/2/13/info2_eng.gif)**

I suppose this to be very good.

Finally, we can create one more convenience – a function that would clear the entire information. It will be
named info\_clear(). I am sure you know how to use it.

```
void info_clear()
 {
     for ( int n = 0;  n < 5;  n ++ )
       info( n, "" );
  for (     n = 10; n < 15; n ++ )
       info( n, "" );
 }
```

### 4\. Summary

The article described alternative methods of how to keep log files and display information.
Two included files were created to be kept in the "experts/include" folder:
the log.mq4 and the info.mq4. They can be used from any expert.

Translated from Russian by MetaQuotes Ltd.

Original article: [https://www.mql5.com/ru/articles/1405](https://www.mql5.com/ru/articles/1405)

**Attached files** \|


[Download ZIP](https://www.mql5.com/en/articles/download/1405.zip "Download all attachments in the single ZIP archive")

[info.mq4](https://www.mql5.com/en/articles/download/1405/info.mq4 "Download info.mq4")(4.5 KB)

[info\_test\_eng.mq4](https://www.mql5.com/en/articles/download/1405/info_test_eng.mq4 "Download info_test_eng.mq4")(1.24 KB)

[log.mq4](https://www.mql5.com/en/articles/download/1405/log.mq4 "Download log.mq4")(3.19 KB)

[log\_test\_eng.mq4](https://www.mql5.com/en/articles/download/1405/log_test_eng.mq4 "Download log_test_eng.mq4")(0.76 KB)

**Warning:** All rights to these materials are reserved by MetaQuotes Ltd. Copying or reprinting of these materials in whole or in part is prohibited.

This article was written by a user of the site and reflects their personal views. MetaQuotes Ltd is not responsible for the accuracy of the information presented, nor for any consequences resulting from the use of the solutions, strategies or recommendations described.

#### Other articles by this author

- [How to Order an Expert Advisor and Obtain the Desired Result](https://www.mql5.com/en/articles/235)
- [Equivolume Charting Revisited](https://www.mql5.com/en/articles/1504)
- [Testing Visualization: Account State Charts](https://www.mql5.com/en/articles/1487)
- [An Expert Advisor Made to Order. Manual for a Trader](https://www.mql5.com/en/articles/1460)
- [Testing Visualization: Trade History](https://www.mql5.com/en/articles/1452)
- [Sound Alerts in Indicators](https://www.mql5.com/en/articles/1448)
- [Filtering by History](https://www.mql5.com/en/articles/1441)

**Last comments \|**
**[Go to discussion](https://www.mql5.com/en/forum/39201)**
(4)


**-**
\|
11 May 2010 at 06:05

It is helpful.Thanks.

![](https://c.mql5.com/avatar/avatar_na2.png)

**\[Deleted\]**
\|
22 Oct 2011 at 22:57

greet !!


![nduru22](https://c.mql5.com/avatar/avatar_na2.png)

**[nduru22](https://www.mql5.com/en/users/nduru22)**
\|
27 Mar 2013 at 14:20

should [function](https://www.mql5.com/en/docs/constants/namedconstants/compilemacros "MQL5 documentation: Predefined Macrosubstitutions") void\_init() be placed under the init() special function? and the same for void\_deinit()?


![nduru22](https://c.mql5.com/avatar/avatar_na2.png)

**[nduru22](https://www.mql5.com/en/users/nduru22)**
\|
27 Mar 2013 at 14:53

sorry, have been able to do it. great. thanks


![Error 146 ("Trade context busy") and How to Deal with It](https://c.mql5.com/2/17/94_1.gif)[Error 146 ("Trade context busy") and How to Deal with It](https://www.mql5.com/en/articles/1412)

The article deals with conflict-free trading of several experts on one МТ 4 Client Terminal. It will be useful for those who have basic command of working with the terminal and programming in MQL 4.

![Free-of-Holes Charts](https://c.mql5.com/2/13/130_1.png)[Free-of-Holes Charts](https://www.mql5.com/en/articles/1407)

The article deals with realization of charts without skipped bars.

![Working with Files. An Example of Important Market Events Visualization](https://c.mql5.com/2/13/112_2.gif)[Working with Files. An Example of Important Market Events Visualization](https://www.mql5.com/en/articles/1382)

The article deals with the outlook of using MQL4 for more productive work at FOREX markets.

![One-Minute Data Modelling Quality Rating](https://c.mql5.com/2/17/89_1.gif)[One-Minute Data Modelling Quality Rating](https://www.mql5.com/en/articles/1513)

One-Minute Data Modelling Quality Rating

[![](https://www.mql5.com/ff/sh/zf7a2k61x98jzh89z2/01.png)Speed up your tradingUse our high-speed VPS for MetaTrader 4 and 5Learn more](https://www.mql5.com/ff/go?link=https://www.mql5.com/en/vps&a=qtrrsuiwuicrscmckjynyanztbditglq&s=c617dc80d90cfd3783ec1345eec2b419b281f10fec6eac77b3218984ac337259&uid=&ref=https://www.mql5.com/en/articles/1405&id=wdausxxqrpvhekbwjrjlhqjghyhesrqqau&fz_uniq=5083071229407007841)

![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

You are missing trading opportunities:

- Free trading apps
- Over 8,000 signals for copying
- Economic news for exploring financial markets

RegistrationLog in

latin characters without spaces

a password will be sent to this email

An error occurred


- [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

You agree to [website policy](https://www.mql5.com/en/about/privacy) and [terms of use](https://www.mql5.com/en/about/terms)

If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

Allow the use of cookies to log in to the MQL5.com website.

Please enable the necessary setting in your browser, otherwise you will not be able to log in.

[Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

- [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)

This website uses cookies. Learn more about our [Cookies Policy](https://www.mql5.com/en/about/cookies).
---
title: Grokking market "memory" through differentiation and entropy analysis
url: https://www.mql5.com/en/articles/6351
categories: Trading
relevance_score: 0
scraped_at: 2026-01-24T13:34:58.349779
---

[![](https://www.mql5.com/ff/sh/qv94j0cd8n2n55z9z2/01.png)![](https://www.mql5.com/ff/sh/qv94j0cd8n2n55z9z2/02.png)Boost your trading experienceRead our book "MQL5 Programming for Traders"Begin](https://www.mql5.com/ff/go?link=https://www.mql5.com/en/book%3Futm_source=www.mql5.com%26utm_medium=display%26utm_term=read.algobook%26utm_content=visit.page%26utm_campaign=algobook.promo.04.2024&a=heclgjpfbvfghpmyaciuaesdtswflupo&s=4255fbe1b8cbc4d1b40afbaebf4235e5ace8b5103cba60d996897a03d588556f&uid=&ref=https://www.mql5.com/en/articles/6351&id=bfogggabsofabcpxuzmgaibarmaxasdrj&fz_uniq=5082969541761307216)

MetaTrader 5 / Trading


### Reason for the approach use and a brief background

A large number of participants in liquid markets, who operate with different investment horizons, produce a lot of market noise. Thus markets have a low signal-to-noise ratio. The situation can be worsened by attempts of integer time series differentiation, which erases the remaining memory and converts quotes to a series characterized by stationarity.

Price series have memory, because each value depends on a long history of price levels. Time series transformations, such as for example the logarithm of increments, crop the memory because they are created based on a limited window length. When transformations to stationarity remove market memory, statisticians use complex mathematical methods to extract the remaining memory. That is why many of the related classical statistical approaches lead to false results.

### The concept of long-range dependence

Long-range dependence (LRD), also called long memory or long-range persistence, is a phenomenon which may arise in the analysis of financial time series. This is expressed in the decay rate of the statistical dependence between two prices with an increase in the time interval (or the distance between them). A phenomenon is considered to have a long-range dependence, when the dependence decays slower than the exponential decay. Long-range dependency is also often associated with self-similar processes. For details about [LRD](https://en.wikipedia.org/wiki/Long-range_dependence "https://en.wikipedia.org/wiki/Long-range_dependence") ( **Long-Range Dependence**), please read the Wikipedia article.

### The problem of stationarity and existence of memory

A common feature of price charts is the non-stationarity: they have a long history of price levels which shift the average price over time. In order to perform a statistical analysis, researchers need to work with price increments (or logarithms of increments), changes in profitability or volatility. These transformations make the time series stationary by removing the memory from price sequences. Although stationarity is a necessary property for statistical conclusions, it is not always necessary to delete the entire memory, since the memory is the basis for the predictive properties of models. For example, equilibrium (stationary) models must contain some memory in order to enable the evaluation of how far the price has moved away from its expected value.

The problem is that price increments are stationary but do not contain memory of the past, while the price series contains the entire amount of available memory but it is non-stationary. The question arises: how to differentiate a time series to make it stationary and at the same time to preserve the maximum possible amount of memory. Therefore we would like to generalize the concept of price increments for considering stationary series in which not all memory is erased. In this case, price increments are not the optimal solution for price transformation, among other available methods.

For this purpose, the concept of fractional differentiation will be introduced. There is a wide range of possibilities between the two extremes: single and zero differentiation. On its one edge, there are fully differentiated prices. Not differentiated prices are at its other edge.

The scope of use of fractional differentiation is wide enough. For example, a differentiated series is usually input into machine learning algorithms. The problem is that it is necessary to display new data in accordance with the available history, which the machine learning model can recognize. In case of a non-stationary series, new data can be located outside the known range of values, due to which the model may operate incorrectly.

### The history of fractional differentiation

Almost all methods for the analysis and forecasting of financial time series, described in various scientific articles, present the idea of integer differentiation.

The following questions arise in this connection:

- Why is integer differentiation (say, with a unit lag) optimal?
- Isn't such overdifferentiation one of the reasons why the economic theory is prone to hypothesis of efficient markets?

The concept of fractional differentiation, as applied to time series analysis and forecasting, goes back at least to [Hosking](https://www.mql5.com/go?link=http://wwwf.imperial.ac.uk/~ejm/M3S8/Problems/hosking81.pdf "http://wwwf.imperial.ac.uk/~ejm/M3S8/Problems/hosking81.pdf"). In his article, the family of ARIMA processes was generalized allowing the differentiation degree to have the fractional part. This made sense, because the fractional differentiation process revealed long-range persistence or anti-persistence and thus increased the predictive ability compared to standard ARIMA. This model was called [ARFIMA](https://en.wikipedia.org/wiki/Autoregressive_fractionally_integrated_moving_average "https://en.wikipedia.org/wiki/Autoregressive_fractionally_integrated_moving_average")Â ( **autoregressive fractionally integrated moving average**) or FARIMA. After that, fractional differentiation was sometimes mentioned in articles by other authors, mainly in connection with the calculation method acceleration.

Such models can be useful in modeling long-memory time series, i.e. in cases where deviations from the long-range average decay more slowly than the exponential decay.

### The concept of fractional differentiation

Let us consider the backshift operator (or a lag operator)Â **B**, applied to the matrix of real valuesÂ **{Xt}**, where **B^kXt = Xtâˆ’k**, for any integerÂ **k â‰¥ 0**. For example,Â **(1 âˆ’ B)^2 = 1 âˆ’ 2B + B^2**, whereÂ **B^2Xt = Xtâˆ’2**, consequently,Â **(1 âˆ’ B)^2Xt = Xt âˆ’ 2Xtâˆ’1 + Xtâˆ’2**.

Note thatÂ **(x + y)^n =**![](https://c.mql5.com/2/35/snip_20190412124253.png) for each positive integer **n.** For the real number **d**,Â ![](https://c.mql5.com/2/35/snip_20190412124905.png) is a binomial series. In the fractional model, **d** may be a real number with the following formal extension of the binomial series:

![](https://c.mql5.com/2/35/snip_20190412125320.png)

### Preserving market memory in case of fractional differentiation

Let is see how rational non-negative **d** can preserve the memory. This arithmetic series consists of a scalar product:

![](https://c.mql5.com/2/35/snip_20190412125920.png)

with weightsÂ **ğœ”**

![](https://c.mql5.com/2/35/snip_20190412130016.png)

and values **X**

![](https://c.mql5.com/2/35/snip_20190412130143.png)

When **d** is a positive integer,Â ![](https://c.mql5.com/2/35/snip_20190412130416__1.png), memory is cropped in this case.

For example, **d = 1** is used for calculating increments where ![](https://c.mql5.com/2/35/snip_20190412130709.png)Â andÂ **ğœ” = {1,âˆ’1, 0, 0,â€¦}**.

### Fractional differentiation for a fixed observation window

Fractional differentiation is usually applied to the entire sequence of a time series. Complexity of calculation is higher in this case, while the shift of the transformed series is negative. Marcos Lopez De Prado in his book [Advances in Financial Machine Learning](https://www.mql5.com/go?link=https://www.wiley.com/en-aw/Advances+in+Financial+Machine+Learning-p-9781119482086 "https://www.wiley.com/en-aw/Advances+in+Financial+Machine+Learning-p-9781119482086") proposed a method of a fixed-width window, in which the sequence of coefficients is discarded when their module **(\|ğœ”k\|)** becomes less than the specified threshold **(ğœ)**. This procedure has the following advantage over the classical expanding window method: it allows having equal weights for any sequence of the original series, reduces the computational complexity and eliminates the backshift. This conversion allows saving memory about price levels and the noise. The distribution of this transformation is not normal (Gaussian) due to the presence of memory, asymmetry and excess kurtosis, however, it can be stationary.

### Demonstration of fractional differentiation process

Let us create a script which will allow you to visually evaluate the effect obtained from the fractional differentiation of the time series. We will create two functions: one for obtaining weights **ğœ”** and the other for calculating new values of the series:

```
//+------------------------------------------------------------------+
void get_weight_ffd(double d, double thres, int lim, double &w[]) {
Â Â Â Â ArrayResize(w,1);
Â Â Â Â ArrayInitialize(w,1.0);
Â Â Â Â ArraySetAsSeries(w,true);

Â Â Â Â int k = 1;
Â Â Â Â int ctr = 0;
Â Â Â Â double w_ = 0;
Â Â Â Â while (ctr != lim - 1) {
Â Â Â Â Â Â Â Â w_ = -w[ctr] / k * (d - k + 1);
Â Â Â Â Â Â Â Â if (MathAbs(w_) < thres) break;
Â Â Â Â Â Â Â Â ArrayResize(w,ArraySize(w)+1);
Â Â Â Â Â Â Â Â w[ctr+1] = w_;
Â Â Â Â Â Â Â Â k += 1;
Â Â Â Â Â Â Â Â ctr += 1;
Â Â Â Â }
}
//+------------------------------------------------------------------+
void frac_diff_ffd(double &x[], double d, double thres, double &output[]) {
Â Â  double w[];
Â Â  get_weight_ffd(d, thres, ArraySize(x), w);

Â Â  int width = ArraySize(w) - 1;

Â Â  ArrayResize(output, width);
Â Â  ArrayInitialize(output,0.0);
Â Â  ArraySetAsSeries(output,true);
Â Â  ArraySetAsSeries(x,true);
Â Â  ArraySetAsSeries(w,true);

Â Â  int o = 0;
Â Â  for(int i=width;i<ArraySize(x);i++) {
Â Â Â Â Â Â ArrayResize(output,ArraySize(output)+1);

Â Â Â Â Â Â for(int l=0;l<ArraySize(w);l++)
Â Â Â Â Â Â Â Â  output[o] += w[l]*x[i-width+l];
Â Â Â Â Â Â o++; }
Â Â  ArrayResize(output,ArraySize(output)-width);
}
```

Let us display an animated chart which changes depending on the parameter **0<d<1:**

```
//+------------------------------------------------------------------+
//| Script program start functionÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |
//+------------------------------------------------------------------+
void OnStart()
Â Â {
Â Â  for(double i=0.05; i<1.0; plotFFD(i+=0.05,1e-5))
Â Â }
//+------------------------------------------------------------------+
void plotFFD(double fd, double thresh) {
Â Â  double prarr[], out[];
Â Â  CopyClose(_Symbol, 0, 0, hist, prarr);

Â Â  for(int i=0; i < ArraySize(prarr); i++)
Â Â Â Â Â Â prarr[i] = log(prarr[i]);

Â Â  frac_diff_ffd(prarr, fd, thresh, out);
Â Â  GraphPlot(out,1); Sleep(500);
}
```

Here is the result:

![](https://c.mql5.com/2/36/ffd.gif)

Fig. 1. Fractional differentiation 0<d<1

As expected, with an increase in the degree of differentiation **d**, the charts becomes more stationary, while gradually losing the "memory" of past levels. The weights for the series (the function of the scalar product of weights by price values) remain unchanged during the entire sequence and do not need recalculation.

### Creating an indicator based on fractional differentiation

For the convenient use in Expert Advisors, let us create an indicator which we will be able to include, specifying various specified parameters: the degree of differentiation, the size of the threshold for removing excess weights and the depth of the displayed history. I will not post the full indicator code here, so you can view it in the source file.

I will only indicate that the weight calculation function is the same. The following function is used for calculating the indicator buffer values:

```
frac_diff_ffd(weights, price, ind_buffer, hist_display, prev_calculated !=0);
```

![](https://c.mql5.com/2/36/snip_20190603221636.png)

Fig. 2. Fractional differentiation with powers of 0.3 and 0.9

Now we have an indicator which quite accurately explicates the information amount change dynamics in a time series. When the degree of differentiation increases, information is lost and the series becomes more stationary. However, only price level data is lost. What may be left is the periodic cycles which will be the reference point for forecasting. So we are approaching the information theory methods, namely the information entropy, which will help with the assessment of the data amount.

### The concept of information entropy

Informational entropy is a concept related to the information theory, which shows how much information is contained in an event. In general, the more specific or deterministic the event, the less information it will contain. More specifically, information is connected with an increase in uncertainty. This concept was introduced by [Claude Shannon](https://en.wikipedia.org/wiki/Claude_Shannon "https://en.wikipedia.org/wiki/Claude_Shannon").

The entropy of a random value can be determined by introducing the concept of distribution of a random **X** value, which takes a finite number of values:

![](https://c.mql5.com/2/36/snip_20190607000722.png)

Then the specific information of the event (or of the time series) is defined as follows:

![](https://c.mql5.com/2/36/snip_20190607000609.png)

The entropy estimate can be written as follows:

![](https://c.mql5.com/2/36/snip_20190607000630.png)

The unit of measurement of the information amount and the entropy depends in the logarithm base. This may be, for example, bits, nats, trits or hartleys.

We will not describe in detail the Shannon entropy. However, it should be noted that this method is poorly suited for evaluating short and noisy time series. Therefore Steve Pincus and Rudolf Kalman proposed a method called " **ApEn**" (Approximate Entropy) in relation to financial time series. The method was described in detail in the article " [Irregularity, volatility, risk and financial market time series](https://www.mql5.com/go?link=https://www.ncbi.nlm.nih.gov/pmc/articles/PMC518821/ "https://www.ncbi.nlm.nih.gov/pmc/articles/PMC518821/")".

In this article, they considered two forms of price deviation from constancy (description of volatility), which are fundamentally different:

- the first describes a large standard deviation
- the second one shows extreme irregularity and unpredictability

These two forms are completely different and thus such separation is necessary: the standard deviation remains a good estimate of deviation from the measure of centrality, while ApEn provides an estimate of irregularity. Moreover, the degree of variability is not very critical, while irregularity and unpredictability are really a problem.

Here is a simple example with two time series:

- series (10,20,10,20,10,20,10,20,10,20,10,20...) with alternating 10 and 20
- series (10,10,20,10,20,20,20,10,10,20,10,20...) where 10 and 20 are selected randomly, with a probability of 1/2

The moments of statistics, such as the mean and the variance, will not show differences between the two time series. At the same time, the first series is completely regular. It means that knowing the previous value, you can always predict the next one. The second series is absolutely random, so any attempt to predict will fail.

Joshua Richman and Randall Moorman criticized the **ApEn** method in their article " [Physiological time-analysis analysis using approximate entropy and sample entropy](https://www.mql5.com/go?link=https://www.physiology.org/doi/full/10.1152/ajpheart.2000.278.6.h2039 "https://www.physiology.org/doi/full/10.1152/ajpheart.2000.278.6.h2039")". Instead, they suggested an improved " **SampEn**" method. In particular, they criticized the dependence of the entropy value on the sample length, as well as the inconsistency of values for different but related time series. Also, the newly offered calculation method is less complex. We will use this method and we will describe the features of its application.

### "Sample Entropy" method for determining the regularity of price increments

**SampEn** is a modification of the **ApEn** method. It is used to evaluate the complexity (irregularity) of a signal (time series). For the specified size of **m** points, the **r** tolerance and the **N** values being calculated, **SampEn** is the logarithm of the probability of that if two series of simultaneous points with the length m have the distance < **r**, then the two series of simultaneous points of length **m + 1** also have the distance < **r**.

Suppose that we have a data set of time series with the length of ![](https://c.mql5.com/2/36/snip_20190614104123.png), with a constant time interval between them. Let us define the vector template of length m so thatÂ ![](https://c.mql5.com/2/36/snip_20190614104139.png)Â and the distance function ![](https://c.mql5.com/2/36/snip_20190614104155.png)(iâ‰ j) by Chebyshev, which is the maximum modulus of the difference between these vectors' components (but this can also be another distance function). Then, **SampEn** will be defined as follows:

![](https://c.mql5.com/2/36/snip_20190614104220.png)

Where:

- **A** is the number of pairs of template vectors, in whichÂ ![](https://c.mql5.com/2/36/snip_20190614105618.png)
- **B** is the number of pairs of template vectors, in whichÂ ![](https://c.mql5.com/2/36/snip_20190614105632.png)

It is clear from the above that **A** is always <= **B** and therefore the **SampEn** is always a zero or a positive value. The lower the value, the greater the [self-similarity](https://en.wikipedia.org/wiki/Self-similarity "https://en.wikipedia.org/wiki/Self-similarity") in the data set and the less the noise.

Mainly the following values are used: **m** = 2 and **r** = 0.2 \* std, where std is the standard deviation which should be taken for a very large data set.

I found the quick implementation of the method proposed in the below code and rewrote it in MQL5:

```
double sample_entropy(double &data[], int m, double r, int N, double sd)
{
Â Â int Cm = 0, Cm1 = 0;
Â Â double err = 0.0, sum = 0.0;

Â Â err = sd * r;

Â Â for (int i = 0; i < N - (m + 1) + 1; i++) {
Â Â Â Â for (int j = i + 1; j < N - (m + 1) + 1; j++) {
Â Â Â Â Â Â bool eq = true;
Â Â Â Â Â Â //m - length series
Â Â Â Â Â Â for (int k = 0; k < m; k++) {
Â Â Â Â Â Â Â Â if (MathAbs(data[i+k] - data[j+k]) > err) {
Â Â Â Â Â Â Â Â Â Â eq = false;
Â Â Â Â Â Â Â Â Â Â break;
Â Â Â Â Â Â Â Â }
Â Â Â Â Â Â }
Â Â Â Â Â Â if (eq) Cm++;

Â Â Â Â Â Â //m+1 - length series
Â Â Â Â Â Â int k = m;
Â Â Â Â Â Â if (eq && MathAbs(data[i+k] - data[j+k]) <= err)
Â Â Â Â Â Â Â Â Cm1++;
Â Â Â Â }
Â Â }

Â Â if (Cm > 0 && Cm1 > 0)
Â Â Â Â return log((double)Cm / (double)Cm1);
Â Â else
Â Â Â Â return 0.0;
}
```

In addition, I suggest an option for calculating the cross-sample entropy ( **cross-SampEn**) for cases when it is necessary to obtain an entropy estimate for two series (two input vectors). However it can also be used for calculating the sample entropy:

```
// Calculate the cross-sample entropy of 2 signals
// u : signal 1
// v : signal 2
// m : length of the patterns that compared to each other
// r : tolerance
// return the cross-sample entropy value
double cross_SampEn(double &u[], double &v[], int m, double r) {
Â Â Â Â double B = 0.0;
Â Â Â Â double A = 0.0;
Â Â Â Â if (ArraySize(u) != ArraySize(v))
Â Â Â Â Â Â Â Â Print("Error : lenght of u different than lenght of v");
Â Â Â Â int N = ArraySize(u);

Â Â Â Â for(int i=0;i<(N-m);i++)
Â Â Â Â Â Â {
Â Â Â Â Â Â Â Â  for(int j=0;j<(N-m);j++)
Â Â Â Â Â Â Â Â Â Â  {
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  double ins[]; ArrayResize(ins, m); double ins2[]; ArrayResize(ins2, m);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ArrayCopy(ins, u, 0, i, m); ArrayCopy(ins2, v, 0, j, m);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  B += cross_match(ins, ins2, m, r) / (N - m);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ArrayResize(ins, m+1); ArrayResize(ins2, m+1);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ArrayCopy(ins, u, 0, i, m + 1); ArrayCopy(ins2, v, 0, j, m +1);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â  A += cross_match(ins, ins2, m + 1, r) / (N - m);
Â Â Â Â Â Â Â Â Â Â  }
Â Â Â Â Â Â }

Â Â Â Â B /= N - m;
Â Â Â Â A /= N - m;
Â Â Â Â return -log(A / B);
}
// calculation of the matching number
// it use in the cross-sample entropy calculation
double cross_match(double &signal1[], double &signal2[], int m, double r) {
Â Â Â Â // return 0 if not match and 1 if match
Â Â Â Â double darr[];
Â Â Â Â for(int i=0; i<m; i++)
Â Â Â Â Â Â {
Â Â Â Â Â Â Â Â  double ins[1]; ins[0] = MathAbs(signal1[i] - signal2[i]);
Â Â Â Â Â Â Â Â  ArrayInsert(darr, ins, 0, 0, 1);
Â Â Â Â Â Â }
Â Â Â Â if(darr[ArrayMaximum(darr)] <= r)Â Â return 1.0; else return 0.0;
}
```

The first calculation method is enough and thus it will be further used.

### Persistence and fractional Brownian motion model

If the value of the price series increment is currently increasing, then what is the probability of a continued growth at the next moment? Let us now consider persistence. The measurement of persistence can be of great assistance. In this section we will consider the **SampEn** method application to the evaluation of persistence of increments in a sliding window. This evaluation method was proposed in the aforementioned article " [Irregularity, volatility, risk and financial market time series](https://www.mql5.com/go?link=https://www.ncbi.nlm.nih.gov/pmc/articles/PMC518821/ "https://www.ncbi.nlm.nih.gov/pmc/articles/PMC518821/")".

We already have a differentiated series according to the fractional Brownian motion theory (this is where the "fractional differentiation" term comes from). Define a coarse-grained binary incremental series

**BinInci:= +1, ifÂ _di_ +1Â â€“Â _di_****\> 0, â€“1**.Â Simply put, binarize the increments into the range of +1, -1. Thus we estimate directly the distribution of four possible variants of the increments behavior:

- Up, Up
- Down, Down
- Up, Down
- Down, Up

The independence of the estimates and the statistical power of the method are connected with the following feature: almost all processes have extremely small **SampEn** errors for the **Binlnci** series. A more important fact is that the estimate does not imply and does not require the data to correspond to a Markov chain and it does not require a prior knowledge of any other characteristics except stationarity. If the data satisfies the first-order Markov property, then **SampEn**(1) = **SampEn**(2) which enable drawing of additional conclusions.

The model of Fractional Brownian motion goes back to Benoit Mandelbrot, who modeled phenomena which demonstrated both long-range dependence or "memory" and "heavy tails". This also led to the emergence of new statistical applications, such as the Hurst index and R/S analysis. As we already know, price increments sometimes exhibit long-range dependency and heavy tails.

Thus we can directly evaluate the persistence of a time series: the lowest **SampEn** values will correspond to the largest persistence values and vice versa.

### Implementation of persistence evaluation for a differentiated series

Let us rewrite the indicator and add the possibility to run in the persistence evaluation mode. Since the entropy estimate works for discrete values, we need to normalize the increment values with an accuracy of up to 2 digits.

The full implementation is available in the attached "fractional entropy" indicator. The indicator settings are described below:

```
input boolÂ Â  entropy_eval = true; // show entropy or increment values
input double diff_degree = 0.3;   // the degree of time series differentiation
input double treshhold = 1e-5;    // threshold to cut off excess weight (the default value can be used)
input intÂ Â Â Â hist_display = 5000; // depth of the displayed history
input int    entropy_window = 50; // sliding window for the process entropy evaluation
```

The below figure shows the indicator in two modes (the upper part shows entropy, the lower one features standardized increments):

![](https://c.mql5.com/2/36/snip_20190614132921.png)

Fig. 3. Entropy values for the sliding window 50 (above) and fractional differentiation with the degree of 0.8

It can be seen that the values of these two estimates are not correlated, which is a good sign for the machine learning model (absence of multicollinearity) which will be considered in the next section.

### On-the-fly Expert Advisor optimization using machine learning: logit regression

Thus we have a suitable differentiated time series which can be used for generating trading signals. It was mentioned above that the time series is more stationary and is more convenient for machine learning models. We also have the series persistence evaluation. Now we need to select an optimal machine learning algorithm. Since the EA must be optimized within itself, there is a requirement for the learning speed, which must be very fast and must have minimal delays. For these reasons I have chosen logistic regression.

Logistic regression is used to predict the probability of an event based on the values of a set of variables **x1, x2, x3 ... xN,** which are also called predictors or regressors. In our case, the variables are the indicator values. It is also necessary to introduce the dependent variable **y** which is usually equal to either 0 or 1. Thus, it can serve as a signal to buy or to sell. Based on the values of the regressors, calculate the probability of that the dependent variable belongs to a particular class.

An assumption is made that the probability of occurrence of **y = 1** is equal to: ![](https://c.mql5.com/2/36/snip_20190603230803.png) where ![](https://c.mql5.com/2/36/snip_20190603231034.png) are the vectors of values of independent variables **1, x1, x2 ... xN** and the regression coefficients, respectively, and **f(z)** is the logistic function, or sigmoid: ![](https://c.mql5.com/2/36/snip_20190603231543.png) As a result, the **y** distribution function for the given **x** can be written as follows:![](https://c.mql5.com/2/36/snip_20190604000244.png)

![](https://c.mql5.com/2/36/Logistic-curve.png)

Fig. 4. Logistic curve (sigmoid). Source: [Wikipedia](https://en.wikipedia.org/wiki/Logistic_regression "https://en.wikipedia.org/wiki/Logistic_regression").

We will not describe in detail the logistic regression algorithm, because it is widely known. Let us use the ready-made CLogitModel class from the Alglib library.

### **Creating an Auto Optimizer class**

Let us create a separate class **CAuto\_optimizer** which will represent a combination of the simple virtual tester and the logit regression:

```
//+------------------------------------------------------------------+
//|Auto optimizer classÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |
//+------------------------------------------------------------------+
class CAuto_optimizer
Â Â {
private:
// Logit regression model |||||||||||||||
Â Â  CMatrixDoubleÂ Â Â Â  LRPM;
Â Â  CLogitModelÂ Â Â Â Â Â  Lmodel;
Â Â  CLogitModelShellÂ Â Lshell;
Â Â  CMNLReportÂ Â Â Â Â Â Â Â Lrep;
Â Â  intÂ Â Â Â Â Â Â Â Â Â Â Â Â Â  Linfo;
Â Â  doubleÂ Â Â Â Â Â Â Â Â Â Â Â Lout[];
//||||||||||||||||||||||||||||||||||||||||
Â Â  intÂ Â Â Â Â Â Â Â Â Â Â Â Â Â  number_of_samples, relearn_timout, relearnCounter;
Â Â  virtual voidÂ Â Â Â Â Â virtual_optimizer();
Â Â  doubleÂ Â Â Â Â Â Â Â Â Â Â Â lVector[][2];
Â Â  intÂ Â Â Â Â Â Â Â Â Â Â Â Â Â  hnd, hnd1;

public:
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  CAuto_optimizer(int number_of_sampleS, int relearn_timeouT, double diff_degree, int entropy_window) {
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  this.number_of_samples = number_of_sampleS;
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  this.relearn_timout = relearn_timeouT;
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  relearnCounter = 0;
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  LRPM.Resize(this.number_of_samples, 5);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  hnd = iCustom(NULL, 0, "fractional entropy", false, diff_degree, 1e-05, number_of_sampleS, entropy_window);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  hnd1 = iCustom(NULL, 0, "fractional entropy", true, diff_degree, 1e-05, number_of_sampleS, entropy_window);
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â }
Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â  ~CAuto_optimizer() {};
Â Â  doubleÂ Â Â Â Â Â Â Â Â Â Â Â getTradeSignal();
Â Â };
```

The following is created in the //Logit regression model// section: a matrix for the **x** and **y** values, the logit model Lmodel and its auxiliary classes. After training the model, the Lout\[\] array will receive the probabilities of the signal belonging to any of the classes, 0:1.

The constructor received the size of the learning window **number\_of\_samples**, period after which the model will be re-optimized **relearn\_timout**, and the fractional differentiation degree for the **diff\_degree** indicator, as well as the entropy calculation window **entropy\_window**.

Let us consider in detail the virtual\_optimizer() method:

```
//+------------------------------------------------------------------+
//|Virtual testerÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |
//+------------------------------------------------------------------+
CAuto_optimizer::virtual_optimizer(void) {
Â Â  double indarr[], indarr2[];
Â Â  CopyBuffer(hnd, 0, 1, this.number_of_samples, indarr);
Â Â  CopyBuffer(hnd1, 0, 1, this.number_of_samples, indarr2);
Â Â  ArraySetAsSeries(indarr, true); ArraySetAsSeries(indarr2, true);

Â Â  for(int s=this.number_of_samples-1;s>=0;s--) {
Â Â Â Â Â Â LRPM[s].Set(0, indarr[s]);
Â Â Â Â Â Â LRPM[s].Set(1, indarr2[s]);
Â Â Â Â Â Â LRPM[s].Set(2, s);

Â Â Â Â Â Â if(iClose(NULL, 0, s) > iClose(NULL, 0, s+1)) {
Â Â Â Â Â Â Â Â  LRPM[s].Set(3, 0.0);
Â Â Â Â Â Â Â Â  LRPM[s].Set(4, 1.0);
Â Â Â Â Â Â }
Â Â Â Â Â Â else {
Â Â Â Â Â Â Â Â  LRPM[s].Set(3, 1.0);
Â Â Â Â Â Â Â Â  LRPM[s].Set(4, 0.0);
Â Â Â Â Â Â }
Â Â Â Â }

Â Â Â Â CLogit::MNLTrainH(LRPM, LRPM.Size(), 3, 2, Linfo, Lmodel, Lrep);

Â Â Â Â double profit[], out[], prof[1];
Â Â Â Â ArrayResize(profit,1); ArraySetAsSeries(profit, true); profit[0] = 0.0;
Â Â Â Â int pos = 0, openpr = 0;

Â Â Â Â for(int s=this.number_of_samples-1;s>=0;s--) {
Â Â Â Â Â Â double in[3];
Â Â Â Â Â Â in[0] = indarr[s]; in[1] = indarr2[s]; in[2] = s;
Â Â Â Â Â Â CLogit::MNLProcess(Lmodel, in, out);

Â Â Â Â Â Â if(out[0] > 0.5 && !pos) {pos = 1; openpr = s;};
Â Â Â Â Â Â if(out[0] < 0.5 && !pos) {pos = -1; openpr = s;};

Â Â Â Â Â Â if(out[0] > 0.5 && pos == 1) continue;
Â Â Â Â Â Â if(out[0] < 0.5 && pos == -1) continue;

Â Â Â Â Â Â if(out[0] > 0.5 && pos == -1) {
Â Â Â Â Â Â Â Â  prof[0] = profit[0] + (iClose(NULL, 0, openpr) - iClose(NULL, 0, s));
Â Â Â Â Â Â Â Â  ArrayInsert(profit, prof, 0, 0, 1); pos = 0; }

Â Â Â Â Â Â if(out[0] < 0.5 && pos == 1) {
Â Â Â Â Â Â Â Â  prof[0] = profit[0] + (iClose(NULL, 0, s) - iClose(NULL, 0, openpr));
Â Â Â Â Â Â Â Â  ArrayInsert(profit, prof, 0, 0, 1); pos = 0; }
Â Â Â Â }
 GraphPlot(profit);
}
```

The method is obviously very simple and is therefore quick. The first column of the LRPM matrix is filled in a loop with the indicator values + linear trend value (it was added). In the next loop, the current close price is compared to the previous one in order to clarify the probability of a deal: buy or sell. If the current value is greater than the previous one, then there was a buy signal. Otherwise this was a sell signal. Accordingly, the following columns are filled with the values 0 and 1.

Thus this is a very simple tester which is not aimed to optimally select signals but simple reads them at each bar. The tester can be improved by method overloading, which is however beyond the scope of this article.

After that the logit regression is trained using the MNLTrain() method which accepts the matrix, its size, the number of variables **x** (only one variable is passed here for each case), the Lmodel class object to save the trained model to it and auxiliary classes.

After training, the model is tested and displayed in the optimizer window as a balance chart. This is visually efficient allowing to show how the model was trained on the learning sample. But the performance is not analyzed from the algorithmic point of view.

The virtual optimizer is called from the following method:

```
//+------------------------------------------------------------------+
//|Get trade signalÂ Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â Â |
//+------------------------------------------------------------------+
double CAuto_optimizer::getTradeSignal() {
Â Â  if(this.relearnCounter==0)
Â Â Â Â Â Â this.virtual_optimizer();
Â Â  relearnCounter++;
Â Â  if(this.relearnCounter>=this.relearn_timout) this.relearnCounter=0;

Â Â  double in[], in1[];
Â Â  CopyBuffer(hnd, 0, 0, 1, in); CopyBuffer(hnd1, 0, 0, 1, in1);
Â Â  double inn[3]; inn[0] = in[0]; inn[1] = in1[0]; inn[2] = relearnCounter + this.number_of_samples - 1;
Â Â  CLogit::MNLProcess(Lmodel, inn, Lout);
Â Â  return Lout[0];
}
```

It checks the number of bars which have passed since the last training. If the value exceeds the specified threshold, the model should be re-trained. After that the last value of indicators is copied along the units of time which passed since the last training. This is input into the model using the MNLProcess() method which returns the result showing whether the value belongs to a class 0:1, i.e. the trading signal.

### **Creating an Expert Advisor to test the library operation**

Now we need to connect the library to a trading Expert Advisor and add the signal handler:

```
#include <MT4Orders.mqh>
#include <Math\Stat\Math.mqh>
#include <Trade\AccountInfo.mqh>
#include <Auto optimizer.mqh>

input intÂ Â Â Â Â Â  History_depth = 1000;
input doubleÂ Â Â Â FracDiff = 0.5;
input intÂ Â Â Â Â Â  Entropy_window = 50;
input intÂ Â Â Â Â Â  Recalc_period = 100;
sinput doubleÂ Â  MaximumRisk=0.01;
sinput doubleÂ Â  CustomLot=0;
input intÂ Â Â Â Â Â  Stop_loss = 500;Â Â Â Â Â Â Â Â Â Â  //Stop loss, positions protection
input intÂ Â Â Â Â Â  BreakEven = 300;Â Â Â Â Â Â Â Â Â Â  //Break even
sinput intÂ Â Â Â Â Â OrderMagic=666;

static datetime last_time=0;

CAuto_optimizer *optimizer = new CAuto_optimizer(History_depth, Recalc_period, FracDiff, Entropy_window);
double sig1;
```

Expert Advisor settings are simple and include the window size **History\_depth**, i.e. the number of training examples for the auto optimizer. The differentiation degree **FracDiff** and the number of received bars **Recalc\_period** after which the model will be re-trained. Also the **Entropy\_window** setting has been added for adjusting the entropy calculation window.

The last function receives a signal from a trained model and performs trading operations:

```
void placeOrders(){
Â Â  if(countOrders(0)!=0 || countOrders(1)!=0)Â Â  {
Â Â Â Â Â Â for(int b=OrdersTotal()-1; b>=0; b--)
Â Â Â Â Â Â Â Â  if(OrderSelect(b,SELECT_BY_POS)==true) {
Â Â Â Â Â Â Â Â Â Â if(OrderType()==0 && sig1 < 0.5) if(OrderClose(OrderTicket(),OrderLots(),OrderClosePrice(),0,Red)) {};
Â Â Â Â Â Â Â Â Â Â if(OrderType()==1 && sig1 > 0.5) if(OrderClose(OrderTicket(),OrderLots(),OrderClosePrice(),0,Red)) {};
Â Â Â Â Â Â }
Â Â Â Â }

Â Â  if(countOrders(0)!=0 || countOrders(1)!=0) return;
Â Â  if(sig1 > 0.5 && (OrderSend(Symbol(),OP_BUY,lotsOptimized(),SymbolInfoDouble(_Symbol,SYMBOL_ASK),0,0,0,NULL,OrderMagic,INT_MIN)>0)) { return; }
Â Â  if(sig1 < 0.5 && (OrderSend(Symbol(),OP_SELL,lotsOptimized(),SymbolInfoDouble(_Symbol,SYMBOL_BID),0,0,0,NULL,OrderMagic,INT_MIN)>0)) {}
Â Â }
```

If the probability of buying is greater than 0.5, then this is a buy signal and/or a signal to close a sell position. And vice-versa.

### **Testing of the self-optimizing EA and conclusions**

Let's proceed to the most interesting part, i8.e. to tests.

The Expert Advisor was run with the specified hyperparameters without genetic optimization, i.e. almost at random, on the EURUSD pair with the 15-minute timeframe, at Open prices.

![](https://c.mql5.com/2/36/snip_20190614160504.png)

Fig. 5. Tested Expert Advisor settings

![](https://c.mql5.com/2/36/snip_20190614160654.png)

Fig. 6. Results of testing with the specified settings

![](https://c.mql5.com/2/36/snip_20190614161328.png)

Fig. 7. Virtual tester results in the training sample

In this interval, the implementation showed a stable growth, which means that the approach can be interesting for further analysis.

As a result, we tried to achieve three goals in one article, including the following:

- understanding of the market "memory",
- evaluation of memory in terms of entropy,
- development of self-optimizing Expert Advisors.

Translated from Russian by MetaQuotes Ltd.

Original article: [https://www.mql5.com/ru/articles/6351](https://www.mql5.com/ru/articles/6351)

**Attached files** \|


[Download ZIP](https://www.mql5.com/en/articles/download/6351.zip "Download all attachments in the single ZIP archive")

[fractional\_entropy\_trader.mq5](https://www.mql5.com/en/articles/download/6351/fractional_entropy_trader.mq5 "Download fractional_entropy_trader.mq5")(8.84 KB)

[Auto\_optimizer.mqh](https://www.mql5.com/en/articles/download/6351/auto_optimizer.mqh "Download Auto_optimizer.mqh")(4.6 KB)

[fractional\_entropy.mq5](https://www.mql5.com/en/articles/download/6351/fractional_entropy.mq5 "Download fractional_entropy.mq5")(11.45 KB)

**Warning:** All rights to these materials are reserved by MetaQuotes Ltd. Copying or reprinting of these materials in whole or in part is prohibited.

This article was written by a user of the site and reflects their personal views. MetaQuotes Ltd is not responsible for the accuracy of the information presented, nor for any consequences resulting from the use of the solutions, strategies or recommendations described.

#### Other articles by this author

- [Creating a mean-reversion strategy based on machine learning](https://www.mql5.com/en/articles/16457)
- [Fast trading strategy tester in Python using Numba](https://www.mql5.com/en/articles/14895)
- [Time series clustering in causal inference](https://www.mql5.com/en/articles/14548)
- [Propensity score in causal inference](https://www.mql5.com/en/articles/14360)
- [Causal inference in time series classification problems](https://www.mql5.com/en/articles/13957)
- [Cross-validation and basics of causal inference in CatBoost models, export to ONNX format](https://www.mql5.com/en/articles/11147)
- [Metamodels in machine learning and trading: Original timing of trading orders](https://www.mql5.com/en/articles/9138)

**Last comments \|**
**[Go to discussion](https://www.mql5.com/en/forum/318144)**
(119)


![](https://c.mql5.com/avatar/avatar_na2.png)

**\[Deleted\]**
\|
15 Aug 2022 at 19:58

Hi, this is some kind of research different econometric [methods](https://www.mql5.com/en/docs/standardlibrary/technicalindicators/volumeindicators/ciobv "Standard library: CiOBV class"), to show what an entropy does and how it save information in time series, plus optimization with logistic regression. But to use it in raeal trading need more research I think and more different tests.


![ejsantos](https://c.mql5.com/avatar/avatar_na2.png)

**[ejsantos](https://www.mql5.com/en/users/ejsantos)**
\|
26 Nov 2023 at 23:18

After all the fixes mentioned I get a "code generation error" at compile time.


![babbetta89](https://c.mql5.com/avatar/avatar_na2.png)

**[babbetta89](https://www.mql5.com/en/users/babbetta89)**
\|
16 Mar 2025 at 09:23

I understand that in the indicator normalisation of diff. prices is carried out on the basis of once [calculated mean/std](https://www.mql5.com/en/docs/matrix/matrix_statistics/matrix_mean "MQL5 Documentation: function Mean"). But how correct is it to train the model by a previously unknown mean of the whole hist\_display range? Using a sliding window for normalisation is not more plausible?


![](https://c.mql5.com/avatar/avatar_na2.png)

**\[Deleted\]**
\|
16 Mar 2025 at 09:32

**babbetta89 [#](https://www.mql5.com/ru/forum/316065/page10#comment_56179899):**

I understand that in the indicator normalisation of diff. prices is carried out on the basis of once calculated mean/std. But how correct is it to train the model by a previously unknown mean of the whole hist\_display range? Using a sliding window for normalisation is not more plausible?

Need to remember :) yes, you could try your second option.


![](https://c.mql5.com/avatar/avatar_na2.png)

**\[Deleted\]**
\|
16 Mar 2025 at 10:22

We are incapable of looking ahead but we try to fool ourselves and reassure people who are in a slip !


![Library for easy and quick development of MetaTrader programs (part VIII): Order and position modification events](https://c.mql5.com/2/36/MQL5-avatar-doeasy__3.png)[Library for easy and quick development of MetaTrader programs (part VIII): Order and position modification events](https://www.mql5.com/en/articles/6595)

In the previous articles, we started creating a large cross-platform library simplifying the development of programs for MetaTrader 5 and MetaTrader 4 platforms. In the seventh part, we added tracking StopLimit orders activation and prepared the functionality for tracking other events involving orders and positions. In this article, we will develop the class for tracking order and position modification events.

![Price velocity measurement methods](https://c.mql5.com/2/36/Article_Logo__1.png)[Price velocity measurement methods](https://www.mql5.com/en/articles/6947)

There are multiple different approaches to market research and analysis. The main ones are technical and fundamental. In technical analysis, traders collect, process and analyze numerical data and parameters related to the market, including prices, volumes, etc. In fundamental analysis, traders analyze events and news affecting the markets directly or indirectly. The article deals with price velocity measurement methods and studies trading strategies based on that methods.

![Developing a cross-platform grider EA (part II): Range-based grid in trend direction](https://c.mql5.com/2/36/mql5_ea_adviser_grid.png)[Developing a cross-platform grider EA (part II): Range-based grid in trend direction](https://www.mql5.com/en/articles/6954)

In this article, we will develop a grider EA for trading in a trend direction within a range. Thus, the EA is to be suited mostly for Forex and commodity markets. According to the tests, our grider showed profit since 2018. Unfortunately, this is not true for the period of 2014-2018.

![Evaluating the ability of Fractal index and Hurst exponent to predict financial time series](https://c.mql5.com/2/36/fraktal1.png)[Evaluating the ability of Fractal index and Hurst exponent to predict financial time series](https://www.mql5.com/en/articles/6834)

Studies related to search for the fractal behavior of financial data suggest that behind the seemingly chaotic behavior of economic time series there are hidden stable mechanisms of participants' collective behavior. These mechanisms can lead to the emergence of price dynamics on the exchange, which can define and describe specific properties of price series. When applied to trading, one could benefit from the indicators which can efficiently and reliably estimate the fractal parameters in the scale and time frame, which are relevant in practice.

[![](https://www.mql5.com/ff/sh/jup0jccfs9655z9z2/01.png)Learn to create your own robotsRead our book "MQL5 Programming for Traders"Begin](https://www.mql5.com/ff/go?link=https://www.mql5.com/en/book%3Futm_source=www.mql5.com%26utm_medium=display%26utm_term=read.algobook%26utm_content=visit.page%26utm_campaign=algobook.promo.04.2024&a=rsxjstxkzbrlgjjrxaglpezpvrjflnvw&s=7224440013c3dbc50ba9cc078cd015fabca36df446b8e75028d6b30234663872&uid=&ref=https://www.mql5.com/en/articles/6351&id=wdausxxqrpvhekbwjrjlhqjghyhesrqqau&fz_uniq=5082969541761307216)

This website uses cookies. Learn more about our [Cookies Policy](https://www.mql5.com/en/about/cookies).

![close](https://c.mql5.com/i/close.png)

![MQL5 - Language of trade strategies built-in the MetaTrader 5 client terminal](https://c.mql5.com/i/registerlandings/logo-2.png)

You are missing trading opportunities:

- Free trading apps
- Over 8,000 signals for copying
- Economic news for exploring financial markets

RegistrationLog in

latin characters without spaces

a password will be sent to this email

An error occurred


- [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup&amp;reg=1)

You agree to [website policy](https://www.mql5.com/en/about/privacy) and [terms of use](https://www.mql5.com/en/about/terms)

If you do not have an account, please [register](https://www.mql5.com/en/auth_register)

Allow the use of cookies to log in to the MQL5.com website.

Please enable the necessary setting in your browser, otherwise you will not be able to log in.

[Forgot your login/password?](https://www.mql5.com/en/auth_forgotten?return=popup)

- [Log in With Google](https://www.mql5.com/en/auth_oauth2?provider=Google&amp;return=popup)